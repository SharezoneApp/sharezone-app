plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
def hasKeystorePropertiesFile = keystorePropertiesFile.exists()
if (hasKeystorePropertiesFile) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    throw new GradleException("versionCode not found. Define flutter.versionCode in the local.properties file.")
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    throw new GradleException("versionName not found. Define flutter.versionName in the local.properties file.")
}

android {
    namespace "de.codingbrain.sharezone"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        // Recommanded fix for https://github.com/flutter/flutter/issues/58247.
        checkReleaseBuilds false
        
        disable 'InvalidPackage'
    }

    // From https://patrol.leancode.co/getting-started#create-a-simple-integration-test
    testOptions {
        execution "ANDROIDX_TEST_ORCHESTRATOR"
    }


    defaultConfig {
        applicationId "de.codingbrain.sharezone"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        // From https://patrol.leancode.co/getting-started#create-a-simple-integration-test
        testInstrumentationRunner "pl.leancode.patrol.PatrolJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: "true"
    }

    signingConfigs {
        if(hasKeystorePropertiesFile) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            if(hasKeystorePropertiesFile) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "flavor-type"

    productFlavors {
        dev {
            dimension "flavor-type"
            resValue "string", "app_name", "dev Sharezone"
            applicationId "de.codingbrain.sharezone.dev"
            versionNameSuffix "-dev"
        }
        prod {
            dimension "flavor-type"
            resValue "string", "app_name", "Sharezone"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    // These dependencies are required for Flutter integration tests, which run
    // on Firebase Test Lab. Copied these steps from:
    // https://github.com/flutter/flutter/tree/master/packages/integration_test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // From https://patrol.leancode.co/getting-started#create-a-simple-integration-test
    androidTestUtil "androidx.test:orchestrator:1.4.2"
}

apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'
